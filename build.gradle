buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

String mc_version = "1.7.10"
String mod_version = "1.4.0-PRE"
boolean isDrone = System.getenv("DRONE") == "true";
version = "${mod_version}-${mc_version}"
String commit, buildNumber, branch;
if (isDrone) {
    commit = System.getenv("DRONE_COMMIT").take(7)
    buildNumber = System.getenv("DRONE_BUILD_NUMBER")
    branch = System.getenv("DRONE_BRANCH")
    version += "-${commit}-${buildNumber}-${branch}"
}
group = "dk.mrspring" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TheKitchenMod"

minecraft {
    version = "1.7.10-10.13.4.1566-1.7.10"
    assetDir = "eclipse/assets"
    replace '{$version}', mod_version
}

repositories {
    maven {
        name "applecore"
        url "http://www.ryanliptak.com/maven/"
    }
    maven {
        name "chickenbones"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "mobius"
        url "http://mobiusstrip.eu/maven/"
    }
}

dependencies {
    compile "applecore:AppleCore:1.7.10-1.1.0:deobf"
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.140:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.7.47:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.5.118:dev"
    compile "mcp.mobius.waila:Waila:1.5.10_1.7.10"
    compile files("libs/FurnitureMod-3.4.8_1.7.10.jar")
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"

    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

task sourceJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = "src"
}

task devJar(type: Jar) {
    from(sourceSets.main.output)
    classifier = "deobf"
}

import groovy.json.JsonOutput

task buildInfo << {
    String jsonCode = JsonOutput.toJson(new BuildInformation(
            commit: commit,
            build_number: Integer.valueOf(buildNumber),
            branch: branch,
            mod_version: mod_version,
            mc_version: mc_version,
            commit_full: System.getenv("DRONE_COMMIT"),
            build_time: System.currentTimeMillis() / 1000L,
            files: ["deobf", "src"]));
    File infoFile = new File("build/libs/${archivesBaseName}-${project.version}.json")
    println(infoFile.toString());
    infoFile.createNewFile()
    infoFile.setText(jsonCode)
}
buildInfo.enabled = isDrone;
build.finalizedBy buildInfo

class BuildInformation {
    String commit, branch, mod_version, mc_version, commit_full;
    String[] files;
    int build_number;
    long build_time;
}

artifacts {
    archives sourceJar, devJar
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}
